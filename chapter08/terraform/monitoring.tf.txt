resource "helm_release" "kube_prometheus_stack" {
  name             = "prometheus"
  namespace        = "monitoring"
  create_namespace = true

  repository = "https://prometheus-community.github.io/helm-charts"
  chart      = "kube-prometheus-stack"
  version    = "48.1.1"

  values = [
    <<-EOF
    grafana:
      sidecar:
        datasources:
          enabled: true
    EOF
  ]
}

resource "helm_release" "loki_stack" {
  name       = "loki"
  namespace  = "monitoring"
  repository = "https://grafana.github.io/helm-charts"
  chart      = "loki-stack"
  version    = "2.10.2"

  values = [
    <<-EOF
    loki:
      enabled: true
      persistence:
        enabled: true
        size: 10Gi
      config:
        table_manager:
          retention_deletes_enabled: true
          retention_period: 168h
        compactor:
          retention_enabled: true

    promtail:
      enabled: true
      config:
        clients:
          - url: http://loki.monitoring.svc.cluster.local:3100/loki/api/v1/push
    EOF
  ]

  depends_on = [helm_release.kube_prometheus_stack]
}

resource "kubernetes_manifest" "loki_datasource" {
  manifest = {
    apiVersion = "v1"
    kind       = "ConfigMap"
    metadata = {
      name      = "loki-datasource"
      namespace = "monitoring"
      labels = {
        grafana_datasource = "1"
      }
    }
    data = {
      "loki.yaml" = <<-YAML
        apiVersion: 1
        datasources:
        - name: Loki
          type: loki
          url: http://loki.monitoring.svc.cluster.local:3100
          access: proxy
          isDefault: false
      YAML
    }
  }
  depends_on = [
    helm_release.kube_prometheus_stack,
    helm_release.loki_stack,
  ]
}